#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use JSON;
use Path::Tiny;
use Template;
use MetaCPAN::Client;
use URI;
use FindBin '$RealBin';

my $global_cfg = decode_json(path('dashboard.json')->slurp);

my $tt = Template->new({
  INCLUDE_PATH => $global_cfg->{input_dir},
  OUTPUT_PATH  => $global_cfg->{output_dir},
  WRAPPER      => $global_cfg->{wrapper},
  VARIABLES    => {
    linked_badge => {
      cpan      => \&cpan_badge_link,
      gh        => \&gh_badge_link,
      travis    => \&travis_badge_link,
      coveralls => \&coveralls_badge_link,
      codecov   => \&codecov_badge_link,
      kritika   => \&kritika_badge_link,
    },
  },
});

my @authors;
my @urls;

for (glob "$RealBin/authors/*.json") {
  push @authors, do_author($tt, $_, $global_cfg);
  push @urls, "https://$global_cfg->{domain}/$authors[-1]";
}

$tt->process($global_cfg->{index_template}, { authors => \@authors }, 'index.html');
push @urls, "https://$global_cfg->{domain}/";

for (@{ $global_cfg->{page_templates} }) {
  $tt->process("$_.tt", { name => ucfirst $_ }, "$_/index.html");
  push @urls, "https://$global_cfg->{domain}/$_/";
}

@urls = sort @urls;

$tt->process('sitemap.tt', { urls => \@urls}, 'sitemap.xml');

sub do_author {
  my ($tt, $file, $global_cfg) = @_;

  my $cfg = decode_json(path($file)->slurp);

  $cfg->{modules} = [];

  my $mcpan    = MetaCPAN::Client->new;
  my $author   = $mcpan->author($cfg->{author}{cpan});
  my $releases = $author->releases;

  $cfg->{gravatar} = $author->gravatar_url;

  while ( my $rel = $releases->next ) {
    my $mod;
    $mod->{name} = $rel->name;
    $mod->{dist} = $rel->distribution;
    $mod->{date} = (split /T/, $rel->date)[0];
    # Get the repo link.
    # 1. It should be in the "web" key
    # 2. Otherwise, check the "url" key
    $mod->{repo} = $rel->resources->{repository}{web}
      // $rel->resources->{repository}{url};

    next unless valid_repo($mod->{repo});

    $mod->{repo} =~ s[/$][];
    # We need the repo's name. Try to extract it from the URL
    my $repo_uri = URI->new($mod->{repo});
    if ($mod->{repo} =~ /^(http|git)/) {
      my $path = $repo_uri->path;
      @$mod{qw[repo_owner repo_name]} = (split m|/|, $path, 3)[1, 2];
      $mod->{repo_name} =~ s/\.git$// if $mod->{repo} =~ /^git/;
    } else {
      warn "Strange repo for $mod->{name} ($mod->{repo}). Skipping.\n";
      next;
    }
    push @{ $cfg->{modules} }, $mod;
  }

  $cfg->{modules} = [ sort { $a->{name} cmp $b->{name} } @{$cfg->{modules}} ];

  $cfg->{sort} //= {};
  $cfg->{sort}{column} //= 0;
  $cfg->{sort}{column} = 2 if 'date' eq lc $cfg->{sort}{column};
  $cfg->{sort}{direction} //= 'asc';

  $cfg = {
    %$cfg,
    %$global_cfg,
   };

  $tt->process($cfg->{author_template}, $cfg, "$cfg->{author}{cpan}/index.html")
    or die $tt->error;

  return $cfg->{author}{cpan};
}

sub valid_repo {
  my ($repo_uri) = @_;

  return unless defined $repo_uri;

  # Currently we only support Guthub repos
  return $repo_uri =~ m|github\.com/|;
}

sub cpan_badge_link {
  my ($module) = @_;

  return badge_link(
    "https://metacpan.org/release/$module->{dist}",
    "https://img.shields.io/cpan/v/$module->{dist}.svg",
    "CPAN version for $module->{dist}",
  );
}

sub gh_badge_link {
  my ($workflow, $module) = @_;

  return badge_link(
    "https://github.com/$module->{repo_owner}/$module->{repo_name}/actions?query=workflow%3A$workflow",
    "https://github.com/$module->{repo_owner}/$module->{repo_name}/workflows/$workflow/badge.svg",
    "GH Action $workflow",
  );
}

sub travis_badge_link {
  my ($module) = @_;

  return badge_link(
    "https://travis-ci.org/$module->{repo_owner}/$module->{repo_name}?branch=master",
    "https://travis-ci.org/$module->{repo_owner}/$module->{repo_name}.svg?branch=master",
    "Build status for $module->{dist}",
  );
}

sub coveralls_badge_link {
  my ($module, $author) = @_;

  return badge_link(
    "https://coveralls.io/github/$author->{github}/$module->{repo_name}?branch=master",
    "https://coveralls.io/repos/$author->{github}/$module->{repo_name}/badge.svg?branch=master&service=github",
    "Test coverage for $module->{dist}",
  );
}

sub codecov_badge_link {
  my ($module) = @_;

  return badge_link(
    "https://codecov.io/gh/$module->{repo_owner}/$module->{repo_name}",
    "https://codecov.io/gh/$module->{repo_owner}/$module->{repo_name}/branch/master/graph/badge.svg",
    "Test coverage for $module->{dist}",
  );
}

sub kritika_badge_link {
  my ($module, $author) = @_;

  return badge_link(
    "https://kritika.io/users/$author->{github}/repos/$module->{repo_owner}+$module->{repo_name}/",
    "https://kritika.io/users/$author->{github}/repos/$module->{repo_owner}+$module->{repo_name}/heads/master/status.svg",
    "Kritika grade for $module->{dist}",
  );
}

sub badge_link {
  my ($link_url, $img_url, $alt_text) = @_;

  return qq[<a href="$link_url"><img class="backup_picture" alt="$alt_text" src="$img_url"></a>];
}
